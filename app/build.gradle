apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    
    defaultConfig {
        applicationId "com.github.techisfun.slidingswitch.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.gitVersionCode
        versionName rootProject.ext.gitVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // set the name of the generated apk in the form
        // <projectName>-<versionName>.apk
        setProperty("archivesBaseName", "$project.name-$versionName")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'NewApi' // gradle build will compile code use `Objects.requireNonNull()` for lambda
        abortOnError true
        xmlReport true
        xmlOutput project.file('build/reports/lint/lint-report.xml')
        htmlReport true
        htmlOutput project.file('build/reports/lint/lint-report.html')
        lintConfig file("${project.rootDir}/quality/lint/lint.xml")
    }
    testOptions.unitTests {
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
            jvmArgs '-noverify'
        }
        returnDefaultValues = true
    }
}

dependencies {
    implementation project(':library')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation rootProject.ext.appLibraries.supportAppCompat

    testImplementation rootProject.ext.appTest.junit
    testImplementation rootProject.ext.appTest.mockito

    androidTestImplementation rootProject.ext.appTest.espresso
    androidTestImplementation rootProject.ext.appTest.espressoContrib
    androidTestImplementation rootProject.ext.appTest.rules
    androidTestImplementation rootProject.ext.appTest.junit
}
