apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.github.techisfun.slidingswitch"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.gitVersionCode
        versionName rootProject.ext.gitVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // set the name of the generated apk in the form
        // <projectName>-<versionName>.apk
        setProperty("archivesBaseName", "$project.name-$versionName")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'NewApi' // gradle build will compile code use `Objects.requireNonNull()` for lambda
        abortOnError true
        xmlReport true
        xmlOutput project.file('build/reports/lint/lint-report.xml')
        htmlReport true
        htmlOutput project.file('build/reports/lint/lint-report.html')
        lintConfig file("${project.rootDir}/quality/lint/lint.xml")
    }
    testOptions.unitTests {
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
            jvmArgs '-noverify'
        }
        returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile(rootProject.ext.appTest.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile rootProject.ext.appLibraries.supportAppCompat
    compile rootProject.ext.appLibraries.constraintLayout
    compile rootProject.ext.appLibraries.timber
    compile rootProject.ext.appLibraries.butterknife
    annotationProcessor rootProject.ext.appLibraries.butterknifeProcessor

    testCompile rootProject.ext.appTest.junit
    testCompile rootProject.ext.appTest.mockito
}
